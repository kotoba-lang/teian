(ns teian.query-test
  (:require [clojure.test :refer [deftest is testing]]
            [teian.query :as query]
            [teian.store :as store]))

(defn- backends [] [["MemStore" (store/seed-db)] ["DatomicStore" (store/datomic-seed-db)]])

(deftest draft-status-and-published?
  (doseq [[label s] (backends)]
    (testing label
      (is (= "none" (query/draft-status s "act-board")))
      (is (not (query/published? s "act-board")))

      (store/record-datom! s {:kind :draft :id "act-board" :value {:status :proposed}})
      (is (= "proposed" (query/draft-status s "act-board")))
      (is (not (query/published? s "act-board")) "proposed is not published")

      (store/record-datom! s {:kind :draft :id "act-board" :value {:status :published}})
      (is (= "published" (query/draft-status s "act-board")))
      (is (query/published? s "act-board"))

      (is (= "none" (query/draft-status s "act-never-drafted")))
      (is (not (query/published? s "act-never-drafted")) "deny-by-default for never-drafted artifacts"))))
